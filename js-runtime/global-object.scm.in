;; Copyright (c) 2007-2011, Florian Loitsch
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;    * Redistributions of source code must retain the above copyright
;;      notice, this list of conditions and the following disclaimer.
;;    * Redistributions in binary form must reproduce the above copyright
;;      notice, this list of conditions and the following disclaimer in the
;;      documentation and/or other materials provided with the distribution.
;;    * Neither the name of the <organization> nor the
;;      names of its contributors may be used to endorse or promote products
;;      derived from this software without specific prior written permission.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
;; ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
;; WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
;; DISCLAIMED. IN NO EVENT SHALL <COPYRIGHT HOLDER> BE LIABLE FOR ANY
;; DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
;; (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
;; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;; ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
;; (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
;; SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

(module jsre-global-object
   (import jsre-base-object
	   jsre-ht-object
	   jsre-property-entry
	   jsre-base-string)
   (use jsre-undefined
	jsre-Error
	jsre-Object
	jsre-Date
	jsre-String
	jsre-Bool
	jsre-Number
	jsre-Function
	jsre-conversion
	jsre-Eval-env)
   (static (final-class Js-Global::Js-HT-Object))
   (export *js-global-object* ;::Js-Object
	   *js-global-this* ;::Js-Object ;; alias for js-global-object
	   *js-global-env*
	   ;; reuse the Property-entry
 	   (wide-class Js-Global-Box::Property-entry
 	      id::@JS_STRING@)
	   (global-object-init)
 	   (macro create-declared-global)
 	   (macro create-implicit-global)
 	   (macro create-special-global)
 	   (macro create-runtime-global)
 	   (macro global-read)
 	   (macro global-typeof-read)
 	   (macro global-set!)
	   (macro global-declared?)
	   (create-global::Js-Global-Box id::@JS_STRING@
					 attributes declared? . Linit-val)))

(define-macro (create-declared-global id . Linit-val)
   `(create-global ,id (declared-attributes) #t ,@Linit-val))

(define-macro (create-implicit-global id)
   `(create-global ,id (implicit-attributes) #f))

(define-macro (create-special-global id attributes . Linit-val)
   `(create-global ,id ,attributes #t ,@Linit-val))

(define-macro (create-runtime-global id . Linit-val)
   `(create-global ,id (runtime-attributes) #t ,@Linit-val))

(define-macro (global-read v)
   (let ((tmp (gensym 'tmp)))
   `(let ((,tmp (Property-entry-val ,v)))
       (when (not ,tmp)
	  (undeclared-error (Js-Global-Box-id ,v)))
       (unmangle-false ,tmp))))

(define-macro (global-declared? v)
   `(and (Property-entry-val ,v) #t))

(define-macro (global-typeof-read v)
   (let ((tmp (gensym 'tmp)))
      `(let ((,tmp (Property-entry-val ,v)))
	  (if (not ,tmp)
	      (js-undefined)
	      (unmangle-false ,tmp)))))

;; always returns the set value.
(define-macro (global-set! v val)
   (let ((tmp-val (gensym 'tmp-val)))
      `(let ((,tmp-val ,val))
	  (if (not (global-declared? ,v))
	      (js-property-generic-set! *js-global-this*
					(Js-Global-Box-id ,v)
					(mangle-false ,tmp-val) #f)
	      (Property-entry-val-set! ,v (mangle-false ,tmp-val)))
	  ,tmp-val)))

(define-method (object-display o::Js-Global . Lport)
   (if (null? Lport)
       (display '*js-global-this*)
       (display '*js-global-this* (car Lport))))

(define-method (object-write o::Js-Global . Lport)
   (if (null? Lport)
       (write '*js-global-this*)
       (write '*js-global-this* (car Lport))))

;; all globals will have their box in here.
(define *global-boxes* (make-props-hashtable))

(define *js-global-env* #unspecified)
;; this will be replaced with the correct object. But we need an object pretty
;; soon so we can add the global variables.
(define *js-global-this*
      (co-instantiate ((tmp-global-object (instantiate::Js-Global
					     (props (make-props-hashtable))
					     (proto tmp-global-object))))
	 tmp-global-object))

(define *js-global-object* *js-global-this*) ;; alias for *js-global-this*

(define (create-global id attributes declared? . Lnewval)
   (let ((box (hashtable-get *global-boxes* id)))
      (if (not box)
	  (let ((new-box (instantiate::Property-entry
			    (val (cond
				    ((not declared?) #f)
				    ((null? Lnewval) (js-undefined))
				    (else (mangle-false (car Lnewval)))))
			    (attr attributes))))
	     (widen!::Js-Global-Box new-box
		(id id))
	     (hashtable-put! *global-boxes* id new-box)
	     (when declared?
		(with-access::Js-Global *js-global-this* (props)
		   (hashtable-put! props id new-box)))
	     new-box)
	  (with-access::Js-Global-Box box (attr val)
	     ;; 10.1.3: If a property of the same name exists already then
	     ;; its attributes are used
	     ;; In our case we have one exception: only declared actually
	     ;; exist already.
	     (if (not val)
		 (begin
		    (set! attr attributes)
		    (set! val (cond
				 ((not declared?) #f)
				 ((null? Lnewval) (js-undefined))
				 (else (mangle-false (car Lnewval)))))
		    (when declared?
		       (with-access::Js-Global *js-global-this* (props)
			  (hashtable-put! props id box))))
		 (unless (null? Lnewval)
		    (set! val (mangle-false (car Lnewval)))))
	     box))))


(define (global-object-init)
   (let ((tmp-props-table (Js-Global-props *js-global-object*)))
      (set! *js-global-object* (instantiate::Js-Global
				  (props tmp-props-table)
				  (proto (natO-object-prototype)))))
   (set! *js-global-this* *js-global-object*) ;; alias
   (set! *js-global-env* (instantiate::Js-Eval-env
			    (objs (list *js-global-object*))
			    (next-env #f)))
   (let ((globals-ht (Js-Global-props *js-global-object*)))
      (hashtable-for-each *global-boxes*
			  (lambda (key box)
			     (with-access::Js-Global-Box box (val)
				(when val
				   (hashtable-put! globals-ht key box)))))))

;; ======= implementation of Js-Global.

(define-method (js-class-name::@JS_STRING@ o::Js-Global)
   ;; not defined in ECMA.
   (STR "global"))

(define-method (js-property-one-level-contains? o::Js-Global prop)
   (with-access::Js-Global o (props)
      (and (hashtable-get props prop)
	   #t)))
(define-method (js-property-is-enumerable? o::Js-Global prop)
   (with-access::Js-Global o (props)
      (let ((ht-entry (hashtable-get props prop)))
	 (if ht-entry
	     (with-access::Property-entry ht-entry (attr)
		(with-access::Attributes attr (enumerable)
		   enumerable))
	     #f))))
(define-method (js-property-contains o::Js-Global prop)
   (with-access::Js-Global o (props proto)
      (let* ((ht-entry (hashtable-get props prop))
	     (entry (and ht-entry (Property-entry-val ht-entry))))
	 (cond
	    (entry            entry)
	    ((js-null? proto) #f)
	    (else             (js-property-contains proto prop))))))

(define-method (js-property-generic-set! o::Js-Global prop
					 new-value attributes)
   ;(tprint "set!: " prop " <- " new-value)
   (with-access::Js-Global o (props)
      ;; hashtable-update! is evil! not only is its return value different,
      ;; when weak hashtables are used, it always creates the 'new' entry.
      ;; -> every set automatically creates a new entry. bad bad bad.
      (let ((entry (hashtable-get props prop)))
	 (cond
	    (entry
	     (with-access::Property-entry entry (val attr)
		(if attributes
		    (begin
		       (set! attr attributes)
		       (set! val new-value))
		    (with-access::Attributes attr (read-only)
		       (unless read-only
			  (set! val new-value))))))
	    ((hashtable-get *global-boxes* prop)
	     ;; an implicit variable, or one, that had been deleted.
	     ;; in either case use it.
	     => (lambda (box)
		   (with-access::Property-entry box (val attr)
		      (set! val new-value)
		      ;; implicit-attributes == default-attributes.
		      (set! attr (implicit-attributes)))
		   (hashtable-put! props prop box)))
	    (else
	     (let ((entry (instantiate::Property-entry
			     (val new-value)
			     (attr (or attributes (default-attributes))))))
		(hashtable-put! props prop entry)))))))

(define-method (js-property-safe-delete! o::Js-Global prop)
   (with-access::Js-Global o (props)
      (let ((entry (hashtable-get props prop)))
	 (if (not entry)
	     #t
	     (with-access::Property-entry entry (attr)
		(with-access::Attributes attr (deletable)
		   (if deletable
		       (begin
			  (hashtable-remove! props prop)
			  (if (Js-Global-Box? entry)
			      (with-access::Js-Global-Box entry (val)
				 ;; mark as undeclared and release memory:
				 (set! val #f))
			      (widen!::Deleted-Property entry))
			  #t)
		       #f)))))))

(define-method (js-property-one-level-for-each o::Js-Global p)
   (with-access::Js-Global o (props)
      ;; We can't use hashtable-for-each.
      ;; If during the for-in loop implicitely declared variables become
      ;; "alive" (or dead) they will be visited (or not).
      (let ((els '()))
	 (hashtable-for-each
	  props
	  (lambda (key obj)
	     (cond
		((Deleted-Property? obj) 'do-nothing)
		(else
		 (with-access::Property-entry obj (attr val)
		    (when val
		       (set! els (cons (cons key obj) els))))))))
	 (for-each (lambda (stored)
		      (let ((key (car stored))
			    (obj (cdr stored)))
			 (with-access::Property-entry obj (attr val)
			    (with-access::Attributes attr
				  (read-only deletable enumerable)
			       (p key val read-only deletable enumerable)))))
		   els))))
